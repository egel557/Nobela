null = { "null" }
bool = { "true" | "false" }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
inner = @{ char* }
string = ${ "\"" ~ inner ~ "\"" }
number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

keyword = { "if" | "else" | "call" | "jump" | "set" }
ident = ${ !keyword ~ !null ~ !bool ~ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
value = { null | bool | string | number | ident}

bool_op = { "==" | "!=" | "<=" | ">=" | "<" | ">" }
arith_op = { "+" | "-" | "*" | "/" }
arith_expr = { value ~ (arith_op ~ value)+ | value }
bool_expr = { arith_expr ~ bool_op ~ arith_expr | arith_expr }
expr = { arith_expr | bool_expr }

WHITESPACE = _{ " " }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* }

eol = _{ NEWLINE ~ ("\t"* ~ NEWLINE)* }

indent = _{ PEEK_ALL ~ PUSH("\t") }

speaker = { string }
text = { string }

dialogue = { (((speaker | ident) ~ text) | (text)) ~ (eol ~ PEEK_ALL ~ choice)*}


statement = _{ dialogue | if_stmt | call | jump | set }
choice = { "--" ~ text ~ ("if" ~ bool_expr)? ~ (eol ~ children)?}

if_stmt = { "if" ~ bool_expr ~ ":" ~ (eol ~ children)?}

call = { ("call" | jump) ~ string }

jump = { "jump"}

set = { ident ~ "=" ~ expr}
	
children = _{ indent ~ statement ~ (eol ~ PEEK_ALL ~ statement)* ~ DROP}
timeline = _{ statement ~ (NEWLINE ~ statement)* }
document = _{ SOI ~ eol? ~  timeline ~ eol? ~ "\t"* ~ EOI }