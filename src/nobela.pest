null = { "null" }
bool = { "true" | "false" }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
inner = @{ char* }
string = ${ "\"" ~ inner ~ "\"" }
number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

ident = ${ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
value = { null | bool | string | number | ident}

bool_op = { "==" | "!=" | "<=" | ">=" | "<" | ">" }
arith_op = { "+" | "-" | "*" | "/" }
expr = { value ~ (arith_op ~ value)+ | value }
bool_expr = { expr ~ bool_op ~ expr | expr }

WHITESPACE = _{ " " }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* }

eol = _{ NEWLINE ~ ("\t"* ~ NEWLINE)* }

indent = _{ PEEK_ALL ~ PUSH("\t") }

speaker = { string }
text = { string }

dialogue = { ((speaker ~ text) | (text)) ~ (eol ~ PEEK_ALL ~ choice)*}


statement = _{ dialogue | if_stmt }

choice = { "--" ~ text ~ (eol ~ children)?}

if_stmt = { "if" ~ bool_expr ~ ":" ~ (eol ~ children)?}
	
children = _{ indent ~ statement ~ (eol ~ PEEK_ALL ~ statement)* ~ DROP}
timeline = _{ statement ~ (NEWLINE ~ statement)* }
document = _{ SOI ~ eol? ~  timeline ~ eol? ~ "\t"* ~ EOI }